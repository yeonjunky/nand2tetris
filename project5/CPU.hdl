// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Not(in=instruction[15], out=ainst);
    Not(in=ainst, out=cinst);

    // instruction multiplexor
    Mux16(a=out, b=instruction, sel=ainst, out=i);

    // ARegister
    Or(a=ainst, b=instruction[5], out=ain);
    ARegister(in=i, load=ain, out=a, out[0..14]=addressM);

    // ALU multiplexor
    And(a=cinst, b=instruction[12], out=AM);
    Mux16(a=a, b=inM, sel=AM, out=alumux); // in: aregi, inM 
    // ALU
    ALU(x=dregi, y=alumux, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=out, out=outM, zr=zr, ng=ng); // in: dregi, alumux

    // DRegister
    And(a=cinst, b=instruction[4], out=din);
    DRegister(in=out, load=din, out=dregi);

    // writeM
    And(a=cinst, b=instruction[3], out=writeM);

    // check conditions to jump
    Not(in=ng, out=pos);
    Not(in=zr, out=notzr);
    And(a=ng, b=instruction[2], out=isng);
    And(a=zr, b=instruction[1], out=iszr);
    And(a=pos, b=notzr, out=posnzr);
    And(a=posnzr, b=instruction[0], out=ispos);

    Or(a=isng, b=iszr, out=jump1);
    Or(a=jump1, b=ispos, out=jump2);
    And(a=cinst, b=jump2, out=load);

    Not(in=load, out=inc);

    PC(in=a, load=load, inc=inc, reset=reset, out[0..14]=pc);
}